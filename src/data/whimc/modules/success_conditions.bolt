import whimc:utils as utils

## PUZZLE SUCCESS CONDITIONS

# Succeed if all barrelbots are on green
with utils.add_success_condition("touch_green", "Touch Green"):
    scoreboard players set $success whimc.dummy 1
    execute as @e[type=item_display,tag=whimc.barrelbot,tag=!whimc.dispenser_bot,predicate=whimc:barrelbot/match_id] at @s unless block ~ ~-1 ~ minecraft:lime_concrete run scoreboard players set $success whimc.dummy 0

with utils.add_success_condition("touch_observer", "Touch Observer"):
    scoreboard players set $success whimc.dummy 1
    execute as @e[type=item_display,tag=whimc.barrelbot,tag=!whimc.dispenser_bot,predicate=whimc:barrelbot/match_id] at @s unless block ~ ~-1 ~ minecraft:observer run scoreboard players set $success whimc.dummy 0

# Defer success condition to individual barrelbots
with utils.add_success_condition("defer", "Defer to Barrelbots"):
    scoreboard players set $failure_found whimc.dummy 0
    execute as @e[type=item_display,tag=whimc.barrelbot,tag=!whimc.dispenser_bot,predicate=whimc:barrelbot/match_id] at @s:
        data remove storage whimc:macro success_condition.function
        execute on passengers run data modify storage whimc:macro success_condition.function set from entity @s data.success_function
        with storage whimc:macro success_condition:
            scoreboard players set $success whimc.dummy 1
            $function whimc:barrelbot/bot_success_conditions/$(function)
            execute if score $success whimc.dummy matches 0 run scoreboard players set $failure_found whimc.dummy 1

    scoreboard players set $success whimc.dummy 0
    execute if score $failure_found whimc.dummy matches 0 run scoreboard players set $success whimc.dummy 1


## BOT SUCCESS CONDITIONS

# Succeeds if the barrelbot is on lime concrete
with utils.add_bot_success_condition("touch_green", "Touch Green"):
    scoreboard players set $success whimc.dummy 0
    execute if block ~ ~-1 ~ lime_concrete run scoreboard players set $success whimc.dummy 1

with utils.add_bot_success_condition("touch_blue", "Touch Blue"):
    scoreboard players set $success whimc.dummy 0
    execute if block ~ ~-1 ~ light_blue_concrete run scoreboard players set $success whimc.dummy 1

with utils.add_bot_success_condition("touch_observer", "Touch Observer"):
    execute store result score $success whimc.dummy if block ~ ~-1 ~ observer


utils.create_success_conditions_page()
utils.create_bot_success_conditions_page()